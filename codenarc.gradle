configurations {
    codenarc
}

dependencies {
    String groovyVersion = '3.0.18'

    codenarc group: 'org.codehaus.groovy', name: 'groovy-templates', version: groovyVersion
	codenarc group: 'org.codehaus.groovy', name: 'groovy-xml', version: groovyVersion
	codenarc group: 'org.slf4j', name: 'slf4j-api', version: '1.7.35'
	codenarc group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.12'
    codenarc group: 'org.codenarc', name: 'CodeNarc', version: '3.2.0'
}

tasks.register('codenarcTest') {
    String reportsFile = 'build/reports/codenarc.html'
    inputs.files(fileTree('src/test/groovy'))
    outputs.file("$projectDir/$reportsFile")

    doLast {
        ant.taskdef(name:'codenarc', 
        			classname:'org.codenarc.ant.CodeNarcTask', 
        			classpath: configurations.codenarc.asPath)
        ant.codenarc(
           ruleSetFiles: 'rulesets/imports.xml,rulesets/unused.xml',
           maxPriority1Violations: 0, maxPriority2Violations: 0,
           maxPriority3Violations: 0,
        ) {
           fileset(dir:'src/test/groovy') {
               include(name: '**/*.groovy')
               include(name: '**/*.dummy')
           }

           report(type:'html') {
               option(name: 'outputFile', value: reportsFile)
           }
        }
    }
}