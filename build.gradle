plugins {
	id 'java'
	id 'groovy'
	id 'jacoco'
	id 'com.diffplug.spotless' version '6.18.0'
}

repositories {
	mavenCentral()
}

dependencies {
	def lombokVersion = '1.18.24'

	annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
	implementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.1'
	implementation group: 'org.springframework', name: 'spring-oxm', version: '5.3.27'

	testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.1-groovy-3.0'
}

test {
	useJUnitPlatform()
	testLogging {
		showStandardStreams = Boolean.getBoolean('all.logs')
		events 'passed', 'skipped'
	}
	finalizedBy(jacocoTestReport)
}

jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}
}

spotless {
	groovyGradle {
		target '*.gradle'
		greclipse()
	}
}